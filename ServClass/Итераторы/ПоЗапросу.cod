class inherited Итераторы.Итератор "Итератор, основанный на объекте запроса";

inclass
  func New(Records: class[] Record; Filter: String=""; xArgs:Storage=nil): Итераторы.ПоЗапросу;
    var i: Integer;
    Result = create;
    Result.Q = Query.Create(Records);
    Result.Q.Filter = Filter;
    if xArgs <> nil then
      for i  = 1 .. xArgs.Count do
        Result.Q.SetField(xArgs.IndexByNumber[i], xArgs.ItemsByNumber[i]);
      end;
    end;
    Result.prefirst = True;
    Result.Q.Select;
  end;

  func FromQuery(Q: Query): Итераторы.ПоЗапросу;
    --Итератор по полностью подготовленному и сделанному запросу.
    Result = create;
    Result.Q = Q;
  end;

inobject private
  var Q: Query;
  var prefirst: Logical;

inobject public
  func Текущий synonym Current: Variant;
     -- Возвращает текущий элемент итератора
     return Q.Current;
  end;

  func НаСледующий synonym MoveNext: Logical;
  -- Переводит указатель на следующий элемент и возващает Истина
  -- Если это невозможно - возвращает Ложь
    if prefirst then
      prefirst = False;
    else
      Q.Next;
    fi;
    return not Q.EOF;
  end;

  func Сброс synonym Reset: Logical;
     Q.First;
     self.prefirst = True;
     return true;
  end;

end